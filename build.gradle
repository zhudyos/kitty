plugins {
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.41"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.github.hierynomus.license" version "0.14.0"
    id "org.jetbrains.dokka" version "0.9.18"
}

apply plugin: "maven"
apply plugin: "signing"
apply plugin: "io.spring.dependency-management"

group "io.zhudy.kitty"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

configurations {
    testImplementation.extendsFrom compileOnly
}

dependencyManagement {
    imports {
        mavenBom "org.apache.logging.log4j:log4j-bom:2.12.0"
        mavenBom "org.springframework.boot:spring-boot-dependencies:2.2.0.M4"
        mavenBom "org.springframework:spring-framework-bom:5.2.0.M3"
        mavenBom "com.fasterxml.jackson:jackson-bom:2.9.9"
    }

    dependencies {
        dependency "javax.servlet:javax.servlet-api:4.0.1"
        dependency "org.mybatis:mybatis:3.5.1"
        dependency "org.valiktor:valiktor-core:0.8.0"
        dependency "com.github.pagehelper:pagehelper:5.1.8"
        dependency "com.auth0:java-jwt:3.8.0"
        dependency "io.lettuce:lettuce-core:5.1.6.RELEASE"
        dependency "com.github.pagehelper:pagehelper:5.1.8"

        dependency "io.mockk:mockk:1.9.3"
        dependency "org.assertj:assertj-core:3.12.2"
        dependency "org.junit.jupiter:junit-jupiter:5.5.0"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "org.apache.logging.log4j:log4j-api"

    compileOnly "javax.servlet:javax.servlet-api"
    compileOnly "org.springframework:spring-webmvc"
    compileOnly "org.springframework:spring-webflux"
    compileOnly "com.squareup.okhttp3:okhttp:3.9.0"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.valiktor:valiktor-core"
    compileOnly "com.github.pagehelper:pagehelper"
    compileOnly "com.fasterxml.jackson.module:jackson-module-kotlin"
    compileOnly "com.auth0:java-jwt"
    compileOnly "io.lettuce:lettuce-core"

    compileOnly "org.mybatis:mybatis"
    compileOnly "com.github.pagehelper:pagehelper"

    testImplementation "io.mockk:mockk"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.springframework:spring-test"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

license {
    headerURI = com.hierynomus.gradle.license.LicenseBasePlugin.class.getResource("/headers/Apache-2.0").toURI()
    ext {
        year = "2018-${Calendar.getInstance().get(Calendar.YEAR)}"
        author = "the original author or authors"
    }
}

if (project.hasProperty("signing.keyId")) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: System.env.OSSRH_USERNAME, password: System.env.OSSRH_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: System.env.OSSRH_USERNAME, password: System.env.OSSRH_PASSWORD)
                }

                pom.project {
                    name "kitty"
                    artifactId "kitty"
                    packaging "jar"
                    description "zhudy kotlin 开发工具包，内置集成一些常用方法，及 spring、spring-boot 定制内容"
                    url "https://github.com/zhudyos/kitty"

                    scm {
                        url "https://github.com/zhudyos/kitty"
                        connection "scm:git:git@github.com:zhudyos/kitty.git"
                        developerConnection "scm:git:git@github.com:zhudyos/kitty.git"
                    }

                    licenses {
                        license {
                            name "The Apache License, Version 2.0"
                            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id "kevin70"
                            name "Kevin Zou"
                            email "kevinz@weghst.com"
                        }
                    }
                }
            }
        }
    }

}